;;; -*- Lisp -*-

;; Empty configuration: no asdf.asd
(initialize-source-registry
 '(:source-registry :ignore-inherited-configuration))
(load-system :asdf)
;; We didn't find it and got the fallback
(assert-equal nil (system-source-file (find-system :asdf)))

;; Proper configuration: asdf.asd from the source above
(initialize-source-registry
 `(:source-registry
   (:directory ,*asdf-directory*)
   (:directory ,*uiop-directory*)
   :ignore-inherited-configuration))
(load-system :asdf)
;; This time we found it
(assert-pathname-equal (subpathname *asdf-directory* "asdf.asd")
                       (system-source-file (find-system :asdf)))

(defun system-lisp-files (system)
  (loop :for f :in (required-components system :keep-component 'cl-source-file)
        :collect (namestring (enough-pathname (component-pathname f) *asdf-directory*))))

(defun makefile-lisp-files (target)
  (remove-if 'emptyp
             (split-string ;; NB: assumes GNU make
              (run-program `("make" "--quiet" "--no-print-directory" ,target)
                           :output :string :error-output t :directory *asdf-directory*)
              :separator #(#\space #\newline #\return #\tab))))

(defmacro compare-files (system target)
  `(assert-equal (system-lisp-files ,system) (makefile-lisp-files ,target)))

(DBG "Testing that the Makefile and ASDF agree on the order of UIOP files")
(compare-files :uiop "driver-files")

(DBG "Testing that the Makefile and ASDF agree on the order of ASDF/DEFSYSTEM files")
(compare-files :asdf/defsystem "defsystem-files")
